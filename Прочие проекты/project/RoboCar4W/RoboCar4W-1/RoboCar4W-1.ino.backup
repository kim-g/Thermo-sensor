/*
 * Arduino Robot Car 4 Wheel with UltraSonic distance sensor
 * Copyright 2015 Yuriy Tim http://tim4dev.com
 */

/* Sweep by BARRAGAN <http://barraganstudio.com>
 modified 8 Nov 2013 by Scott Fitzgerald
 http://arduino.cc/en/Tutorial/Sweep
*/
/* Adafruit Motor shield library
   copyright Adafruit Industries LLC, 2009
*/

#include <AFMotor.h>
#include <Servo.h>

#define VERSION "RoboCar4W ver.0.1"

boolean debug = true; // для отладки

// определяем моторы
AF_DCMotor motorFrontLeft(2);  //  передний левый
AF_DCMotor motorFrontRight(1); //  передний правый
AF_DCMotor motorRearLeft(3);   //  задний левый
AF_DCMotor motorRearRight(4);  //  задний правый

byte SPEED_CURRENT = 0; // текущая скорость моторов

/*
 * Задержки для езды, поворотов на месте и плавных поворотов.
 * Подбираются экспериментально.
 */
const int DELAY_RUN    = 250;
const int DELAY_ROTATE = 500;
const int DELAY_TURN   = 500;

/*
 * Сервомотор
 */
Servo servo;

// задержка для посылки команд в сервомотор
const int SERVO_DELAY  = 300;

/*
 * Измерения
 */
const int  A_ANG[3] = {140, 90, 40}; // L-F-R (array of angle) массив углов на которые должен поворачиваться серво для замера расстояний
const char A_ANG_CHAR[4] = "LFR"; // для отладки. тот же массив, с указанием направления

// в сантиметрах (distance threshold) Пороги расстояний до препятствия
// Если ближе, то резкий разворот на месте, иначе плавный доворот
const int DST_TRH_TURN = 20;
// Если ближе, то стоп и назад
const int DST_TRH_BACK = 10;

/* пины для подключения HC-SR04 Ultrasonic Module Distance Measuring */
#define SONIC_PIN_TRIG 13
#define SONIC_PIN_ECHO 2
// Detection distance: 2cm--450cm
const int SONIC_DISTANCE_MAX = 450;
const int SONIC_DISTANCE_MIN = 2;



/******************************************
  Main program
******************************************/

void setup() {
  Serial.begin(9600);           // set up Serial library at 9600 bps
  if (debug) Serial.println(VERSION);

  pinMode(SONIC_PIN_TRIG, OUTPUT);
  pinMode(SONIC_PIN_ECHO, INPUT);

  servo.attach(10);  // attaches the servo on pin 9 or 10 (крайний разъем)
  servo.write(90);
  motorInit();
}

void loop() {
  if (debug) {
    Serial.println("\n*** new loop() start ***\n");
    servo.write(90);
    motorStop();
    delay(1500);
  }
  // сравнить измеренные расстояния до препятствий
  // и определить направления движения
  int i, distance, ch;
  int cur_dist = -1;  // расстояние
  int cur_angle = -1;  // угол
  char cur_angle_char;
  if (debug) Serial.print("distance = ");
  for (i = 0; i < 3; i++) {
    // поворот серво
    servo.write( A_ANG[i] );
    delay(SERVO_DELAY);
    // замер расстояния
    distance = measureDistance();
    if (debug)  {
      Serial.print(distance); Serial.print("["); Serial.print( A_ANG_CHAR[i] ); Serial.print("] ");
    }
    // определение бОльшего расстояния до препятствия
    if ( distance > cur_dist )  {
        cur_dist = distance;
        cur_angle = A_ANG[i];
        cur_angle_char = A_ANG_CHAR[i];
    }
  }
  if (debug)  {
    Serial.print("\ncur_dist = "); Serial.println(cur_dist);
    Serial.print("cur_angle= "); /*Serial.println(cur_angle);*/ Serial.println(cur_angle_char);
  }
  // препятствие прямо впереди и так близко что надо ехать назад ?
  if ( (90 == cur_angle) && (cur_dist <= DST_TRH_BACK) ) {
    if (debug) Serial.println("ALARM! Distance too small!!!");
    // стоп
    motorStop();
    // случайно определяем направление заднего хода
    ch = random(1, 3);
    switch (ch) {
      case 1:
        motorTurnBackLeft();
        motorRunBack();
        break;
      case 2:
        motorRunBack();
        break;
      case 3:
        motorTurnBackRight();
        motorRunBack();
        break;
      default:
        motorRunBack();
    }
    return; // начать новый loop()
  }
  // определить направление поворота

  // прямо
  if ( (cur_angle >= 50) && (cur_angle <= 130) )   {
    motorRunForward();
    return; // начать новый loop()
  }
  // направо
  if ( (cur_angle >= 0) && (cur_angle < 50) )   {
    if (cur_dist < DST_TRH_TURN) {
      // резкий поворот (на месте)
      motorRotateRight();
    } else {
      // плавный поворот
      motorTurnRight();
    }
    // налево
  } else if ( (cur_angle > 130) && (cur_angle <= 180) )   {
    if (cur_dist < DST_TRH_TURN) {
      // резкий поворот (на месте)
      motorRotateLeft();
    } else {
      // плавный поворот
      motorTurnLeft();
    }
  }
  motorRunForward();
}



/******************************************
  Functions
******************************************/

// инициализация моторов
void motorInit()  {
  if (debug) Serial.println("motor Init");
  // turn on motor
  motorSetSpeed(150); // скорость мотора 0--255
  motorStop();
}

// движение вперед по прямой
void motorRunForward()  {
  if (debug) Serial.println("Forward");
  motorFrontLeft.run(FORWARD);
  motorFrontRight.run(FORWARD);
  motorRearLeft.run(FORWARD);
  motorRearRight.run(FORWARD);
  delay(DELAY_RUN);
}

// движение назад по прямой
void motorRunBack()  {
  if (debug) Serial.println("Backward");
  motorFrontLeft.run(BACKWARD);
  motorFrontRight.run(BACKWARD);
  motorRearLeft.run(BACKWARD);
  motorRearRight.run(BACKWARD);
  delay(DELAY_RUN);
}

// правый разворот на месте
void motorRotateRight()  {
  if (debug) Serial.println("Rotate R");
  motorFrontLeft.run(FORWARD);
  motorFrontRight.run(BACKWARD);
  motorRearLeft.run(FORWARD);
  motorRearRight.run(BACKWARD);
  delay(DELAY_ROTATE);
}

// правый плавный поворот (при движении вперед)
void motorTurnRight()  {
  if (debug) Serial.println("Turn R");
  motorFrontLeft.run(FORWARD);
  motorFrontRight.run(RELEASE);
  motorRearLeft.run(FORWARD);
  motorRearRight.run(RELEASE);
  delay(DELAY_TURN);
}

// правый плавный поворот (при движении назад)
void motorTurnBackRight()  {
  if (debug) Serial.println("Turn Back R");
  motorFrontLeft.run(BACKWARD);
  motorFrontRight.run(RELEASE);
  motorRearLeft.run(BACKWARD);
  motorRearRight.run(RELEASE);
  delay(DELAY_TURN);
}

// левый разворот на месте
void motorRotateLeft()  {
  if (debug) Serial.println("Rotate L");
  motorFrontLeft.run(BACKWARD);
  motorFrontRight.run(FORWARD);
  motorRearLeft.run(BACKWARD);
  motorRearRight.run(FORWARD);
  delay(DELAY_ROTATE);
}

// левый плавный поворот (при движении вперед)
void motorTurnLeft()  {
  if (debug) Serial.println("Turn L");
  motorFrontLeft.run(RELEASE);
  motorFrontRight.run(FORWARD);
  motorRearLeft.run(RELEASE);
  motorRearRight.run(FORWARD);
  delay(DELAY_TURN);
}

// левый плавный поворот (при движении назад)
void motorTurnBackLeft()  {
  if (debug) Serial.println("Turn Back L");
  motorFrontLeft.run(RELEASE);
  motorFrontRight.run(BACKWARD);
  motorRearLeft.run(RELEASE);
  motorRearRight.run(BACKWARD);
  delay(DELAY_TURN);
}

// стоп резко
void motorStop()  {
  if (debug) Serial.println("Stop");
  motorFrontLeft.run(RELEASE);
  motorFrontRight.run(RELEASE);
  motorRearLeft.run(RELEASE);
  motorRearRight.run(RELEASE);
}

// стоп плавно
void motorStopSlow()  {
  int speed;
  int diff = SPEED_CURRENT / 3; // сбрасываем скорость в 3 приема
  if (debug) Serial.println("Stop slow");
  for (speed = SPEED_CURRENT; speed <= 0; speed -= diff) {
    motorSetSpeed(speed);
    delay(150);
  }
  motorStop(); // тормозим еще раз на всякий случай
}

// разгон плавно
void motorRunSlow()  {
  int speed;
  int diff = (255 - SPEED_CURRENT) / 3; // набираем скорость в 3 приема
  if (debug) Serial.println("Stop slow");
  for (speed = SPEED_CURRENT; speed > 255; speed += diff) {
    motorSetSpeed(speed);
    delay(150);
  }
  motorSetSpeed(255); // устанавливаем максималку еще раз на всякий случай
}

// установить скорость 0--255
void motorSetSpeed(int speed)  {
  // скорость мотора 0--255
  if (speed > 255)
    speed = 255;
  if (speed < 0)
    speed = 0;
  if (debug) {
    Serial.print("Motor set Speed = ");
    Serial.println(speed);
  }
  motorFrontLeft.setSpeed(speed);
  motorFrontRight.setSpeed(speed);
  motorRearLeft.setSpeed(speed);
  motorRearRight.setSpeed(speed);
  // запоминаем текущую скорость
  SPEED_CURRENT = speed;
}

// Возвращает расстояние до препятствия в сантиметрах
int measureDistance()  {
  long duration;
  int  distance;
  /* Для запуска передатчика нужно подать на Trig сигнал, длительностью 10мкс.
   * Передатчик который посылает 8 коротких импульсов с частотой 40kHz.
   * Приемник получает отраженный сигнал и на входе Echo генерируется сигнал,
   * длительность которого равна времени прохождения звукового сигнала.
   */
  digitalWrite(SONIC_PIN_TRIG, LOW); // инициализация перед замером
  delayMicroseconds(3);
  digitalWrite(SONIC_PIN_TRIG, HIGH);
  delayMicroseconds(10);
  digitalWrite(SONIC_PIN_TRIG, LOW);

  duration = pulseIn(SONIC_PIN_ECHO, HIGH);
  // Скорость звука 340 м/с или 29 микросекунд на сантиметр.
  // Звук идет вперед и возвращается назад, таким образом время нужно делить на два
  distance = duration / 58; // = microseconds / 29 / 2

  if (distance < SONIC_DISTANCE_MIN )  // out of range
    return SONIC_DISTANCE_MIN;
  if (distance > SONIC_DISTANCE_MAX )  // out of range
    return SONIC_DISTANCE_MAX;

  return distance;
}
